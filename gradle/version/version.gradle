task bumpMajor {
  doLast {
    ant.propertyfile(file: "${project.rootDir}/gradle/version/version.properties") {
      entry(key: 'major', type: 'int', operation: '+', value: 1)
      entry(key: 'minor', type: 'int', operation: '=', value: 0)
      entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
    printVersionName()
  }
}

task bumpMinor {
  doLast {
    ant.propertyfile(file: "${project.rootDir}/gradle/version/version.properties") {
      entry(key: 'minor', type: 'int', operation: '+', value: 1)
      entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
    printVersionName()
  }
}

task bumpPatch {
  doLast {
    ant.propertyfile(file: "${project.rootDir}/gradle/version/version.properties") {
      entry(key: 'patch', type: 'int', operation: '+', value: 1)
    }
    printVersionName()
  }
}

task version {
  doLast {
    printVersionName()
  }
}

ext.versionName = {
  def versionProps = getVersionProps()
  return "${versionProps['major']}.${versionProps['minor']}.${versionProps['patch']}"
}

def printVersionName() {
  println versionName()
}

def Properties getVersionProps() {
  def versionPropsFile = file("${project.rootDir}/gradle/version/version.properties")
  def Properties versionProps = new Properties()
  versionProps.load(new FileInputStream(versionPropsFile))
  return versionProps
}
