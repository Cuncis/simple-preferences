apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

task copyTestResources(type: Copy) {
  from "${projectDir}/src/test/resources"
  into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

dependencies {

  implementation project(':annotations')
  implementation deps.autoservice
  implementation deps.autocommon
  implementation deps.javapoet
  implementation deps.android
  implementation deps.androidxAnnotations

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.compiletesting
  testImplementation deps.commonsio
  testImplementation deps.android
  testImplementation deps.androidxAnnotations
  testImplementation project.rootProject.ext.ANDROID_JAR
}

group = GROUP_ID
version = VERSION

task sourcesJar(type: Jar) {
  from sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}


publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId GROUP_ID
      artifactId ARTIFACT_ID_PROCESSOR
      version VERSION
    }
  }
}

bintray {
  publications 'MyPublication'
  user bintrayUser
  key bintrayKey
  dryRun = dryRun
  publish = true

  pkg {
    repo = REPO
    userOrg = USER
    name = ARTIFACT_ID_PROCESSOR
    vcsUrl = VCS_URL
    licenses = LICENSES
    version {
      name = VERSION
      desc = 'An annotation processor for SimplePreferences'
    }
  }
}

// Non-Android project: Artifactory Plugin adds only 1 publication
project.ext.publicationName = "maven"
apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
