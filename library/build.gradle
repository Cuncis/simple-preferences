apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 28
    versionCode 1
    versionName VERSION

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {}

    debug {
      debuggable true
      testCoverageEnabled true
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    disable 'InvalidPackage'
  }
}

dependencies {
  api project(':annotations')
  implementation deps.androidxAnnotations

  testImplementation deps.junit
}

group = GROUP_ID
version = VERSION

bintray {
  publications 'MyPublication'
  user bintrayUser
  key bintrayKey
  dryRun = dryRun
  publish = true

  pkg {
    repo = REPO
    userOrg = USER
    name = ARTIFACT_ID_LIBRARY
    vcsUrl = VCS_URL
    licenses = LICENSES
    websiteUrl = WEBSITE
    version {
      name = VERSION
      desc = 'Simple SharedPreferences for Android'
    }
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      artifact sourcesJar
      artifact javadocJar
      artifact "$buildDir/outputs/aar/library-release.aar"
      groupId GROUP_ID
      artifactId ARTIFACT_ID_LIBRARY
      version VERSION

      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        // Iterate over the compile dependencies, adding a <dependency> node for each
        configurations.implementation.allDependencies.each {
          if (it.group != null && it.name != null && it.version != null) {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            // to change annotation module artifactId explicitly
            // ref: https://github.com/dcendents/android-maven-gradle-plugin/issues/9#issuecomment-73550293
            def artifactId = (it.group == GROUP_ID && it.name == "annotations") ? ARTIFACT_ID_ANNOTATIONS : it.name
            dependencyNode.appendNode('artifactId', artifactId)
            dependencyNode.appendNode('version', it.version)
          }
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

// Android Library project: Artifactory Plugin adds 1 publication per Build Type
project.ext.publicationName = "release"
apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
